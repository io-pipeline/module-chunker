plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.2.0'
}

def pipelineBomVersion = findProperty('pipelineBomVersion') ?: '0.1.2-SNAPSHOT'

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    // Import pipeline BOM to align all managed versions
    implementation platform("ai.pipestream:pipeline-bom:${pipelineBomVersion}")

    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-stork'
    
    // REST API dependencies
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-hibernate-validator'
    
    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation libs.json.schema.validator

    // Pipeline libraries (published to Maven Local)
    testImplementation libs.pipestream.grpc.wiremock
    // Pipestream libraries - versions managed entirely by the BOM/catalog
    implementation libs.pipestream.compose.devservices
    implementation libs.pipestream.grpc.stubs
    implementation libs.pipestream.api
    implementation libs.pipestream.data.util
    implementation libs.pipestream.commons
    implementation libs.pipestream.dynamic.grpc
    implementation libs.pipestream.dynamic.grpc.registration.clients

    implementation libs.opennlp.tools
    implementation libs.commons.lang3
}

group = "ai.pipestream.module"
version = "${pipelineBomVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Publishing configuration
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) { publication ->
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact(file("${buildDir}/quarkus-app/quarkus-run.jar")) { artifact ->
                artifact.classifier = 'runner'
                artifact.builtBy tasks.named('quarkusBuild')
            }
            pom { pom ->
                name.set('Module Chunker')
                description.set('Document chunking module for the Pipeline platform')
                url.set('https://github.com/ai-pipestream/module-chunker')
                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipeline Engine Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/module-chunker.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/module-chunker.git')
                    url.set('https://github.com/ai-pipestream/module-chunker')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ai-pipestream/module-chunker")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                mavenContent {
                    releasesOnly()
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}